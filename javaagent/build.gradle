plugins {
    id 'java'
    id 'org.springframework.boot' version '2.5.12'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'de.undercouch.download' version '4.1.1'
}

description = 'OpenTelemetry Example for Java Agent'
ext.moduleName = "io.opentelemetry.examples.javagent"

bootRun {
    mainClass.set 'io.opentelemetry.example.javagent.Application'
}

bootJar {
    dependsOn("downloadAgent")
    dependsOn("downloadDigmaAgentExtension")
}

dependencies {
    implementation("io.opentelemetry:opentelemetry-api")

    //spring modules
    implementation("org.springframework.boot:spring-boot-starter-web")
}

// Download the OpenTelemetry java agent and put it in the build directory
task downloadAgent(type: Download) {
    src "https://github.com/open-telemetry/opentelemetry-java-instrumentation/releases/latest/download/opentelemetry-javaagent.jar"
    dest project.buildDir.toString() + "/otel/opentelemetry-javaagent.jar"
    overwrite true
}

// Download the Digma OpenTelemetry agent extension and put it in the build directory
task downloadDigmaAgentExtension(type: Download) {
    //src "https://s01.oss.sonatype.org/content/repositories/snapshots/io/github/digma-ai/digma-otel-agent-extension/0.4.17-SNAPSHOT/digma-otel-agent-extension-0.4.17-20221127.104057-1-all.jar"
    src "https://s01.oss.sonatype.org/content/repositories/releases/io/github/digma-ai/digma-otel-agent-extension/0.4.22/digma-otel-agent-extension-0.4.22.jar"
    dest project.buildDir.toString() + "/otel/digma-otel-agent-extension.jar"
    overwrite true
}
