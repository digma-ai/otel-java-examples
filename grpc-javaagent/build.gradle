plugins {
    id "com.google.protobuf"
    id 'de.undercouch.download' version '4.1.1'
}

description = 'OpenTelemetry Examples for gRPC with Java Agent'
ext.moduleName = "io.opentelemetry.examples.grpc-javaagent"

dependencies {
    implementation "io.opentelemetry:opentelemetry-api"
    implementation "io.opentelemetry:opentelemetry-sdk"
    implementation "io.opentelemetry:opentelemetry-exporter-logging"
    implementation("io.opentelemetry:opentelemetry-exporter-otlp")

    implementation("io.opentelemetry.instrumentation:opentelemetry-grpc-1.6")

    //alpha module
    implementation "io.opentelemetry:opentelemetry-semconv"

    implementation "io.grpc:grpc-protobuf"
    implementation "io.grpc:grpc-stub"
    implementation "io.grpc:grpc-netty-shaded"

    if (JavaVersion.current().isJava9Compatible()) {
        // Workaround for @javax.annotation.Generated
        // see: https://github.com/grpc/grpc-java/issues/3633
        compileOnly "javax.annotation:javax.annotation-api:1.3.2"
    }
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:3.21.2" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.50.2" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

jar {
    dependsOn("downloadAgent")
    dependsOn("downloadDigmaAgentExtension")
}

// Download the OpenTelemetry java agent and put it in the build directory
task downloadAgent(type: Download) {
    src "https://github.com/open-telemetry/opentelemetry-java-instrumentation/releases/latest/download/opentelemetry-javaagent.jar"
    dest project.buildDir.toString() + "/otel/opentelemetry-javaagent.jar"
    overwrite true
}

// Download the Digma OpenTelemetry agent extension and put it in the build directory
task downloadDigmaAgentExtension(type: Download) {
    src "https://s01.oss.sonatype.org/content/repositories/releases/io/github/digma-ai/digma-otel-agent-extension/0.5.30/digma-otel-agent-extension-0.5.30.jar"
    dest project.buildDir.toString() + "/otel/digma-otel-agent-extension.jar"
    overwrite true
}
